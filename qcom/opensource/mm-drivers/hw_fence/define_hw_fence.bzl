load("//build/kernel/kleaf:kernel.bzl", "ddk_module")
load("//build/bazel_common_rules/dist:dist.bzl", "copy_to_dist_dir")
load("//vendor/qcom/sm8850-modules/qcom/opensource/mm-drivers:target_variants.bzl", "get_all_variants")

def _define_module(target, variant):
    tv = "{}_{}".format(target, variant)

    deps = select({
        "//build/kernel/kleaf:socrepo_true": [
            "//vendor/qcom/kernel:all_headers",
            "//vendor/qcom/kernel:{}/drivers/remoteproc/rproc_qcom_common".format(tv),
            "//vendor/qcom/kernel:{}/drivers/remoteproc/qcom_q6v5_pas".format(tv),
            "//vendor/qcom/kernel:{}/drivers/virt/gunyah/gh_dbl".format(tv),
            "//vendor/qcom/kernel:{}/drivers/virt/gunyah/gh_rm_drv".format(tv),
            "//vendor/qcom/kernel:{}/drivers/firmware/qcom/qcom-scm".format(tv),
        ],
        "//build/kernel/kleaf:socrepo_false": [
            "//vendor/qcom/kernel:all_headers",
        ],
    })
    kernel_build = select({
        "//build/kernel/kleaf:socrepo_true": "//vendor/qcom/kernel:{}_base_kernel".format(tv),
        "//build/kernel/kleaf:socrepo_false": "//vendor/qcom/kernel:{}".format(tv),
    })

    if target in ["pineapple"]:
        target_config = "defconfig"
    else:
        target_config = "{}_defconfig".format(target)

    ddk_module(
        name = "{}_msm_hw_fence".format(tv),
        srcs = [
            "src/hw_fence_drv_debug.c",
            "src/hw_fence_drv_ipc.c",
            "src/hw_fence_drv_priv.c",
            "src/hw_fence_drv_utils.c",
            "src/msm_hw_fence.c",
        ],
        out = "msm_hw_fence.ko",
        defconfig = target_config,
        kconfig = "Kconfig",
        conditional_srcs = {
            "CONFIG_DEBUG_FS": {
                True: ["src/hw_fence_ioctl.c"],
            },
            "CONFIG_QTI_HW_FENCE_USE_SYNX": {
                True: [
                    "src/msm_hw_fence_synx_translation.c",
                    "src/hw_fence_drv_interop.c",
                ],
            },
        },
        deps = deps + [
            "//vendor/qcom/sm8850-modules/qcom/opensource/synx-kernel:synx_headers",
            "//vendor/qcom/sm8850-modules/qcom/opensource/mm-drivers:mm_drivers_headers",
        ],
        kernel_build = kernel_build,
    )

    copy_to_dist_dir(
        name = "{}_msm_hw_fence_dist".format(tv),
        data = [":{}_msm_hw_fence".format(tv)],
        dist_dir = "out/target/product/{}/dlkm/lib/modules".format(target),
        flat = True,
        wipe_dist_dir = False,
        allow_duplicate_filenames = False,
        mode_overrides = {"**/*": "644"},
        log = "info",
    )

def define_hw_fence():
    for (t, v) in get_all_variants():
        _define_module(t, v)
